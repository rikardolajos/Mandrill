#version 460
#extension GL_EXT_ray_tracing : require

layout(set = 0, binding = 0) uniform CameraUniforms {
    mat4 view;
    mat4 view_inv;
    mat4 proj;
    mat4 proj_inv;
} camera;

layout(set = 3, binding = 0) uniform accelerationStructureEXT scene;
layout(set = 4, binding = 0, rgba8) uniform image2D image;

layout(location = 0) rayPayloadEXT vec3 hit_value;

void main()
{
	const vec2 pixel_center = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 in_uv = pixel_center / vec2(gl_LaunchSizeEXT.xy);
	vec2 d = in_uv * 2.0 - 1.0;

	vec4 origin = camera.view_inv * vec4(0.0, 0.0, 0.0, 1.0);
	vec4 target = camera.proj_inv * vec4(d.x, d.y, 1.0, 1.0);
	vec4 direction = camera.view_inv * vec4(normalize(target.xyz), 0.0);

	float t_min = 0.001;
	float t_max = 10000.0;

    hit_value = vec3(0.0);

    traceRayEXT(scene, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin.xyz, t_min, direction.xyz, t_max, 0);

	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(hit_value, 0.0));
}

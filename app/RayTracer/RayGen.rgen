#version 460
#extension GL_EXT_ray_tracing : require

#include "RayPayload.glsl"

layout(set = 0, binding = 0) uniform CameraUniforms {
    mat4 view;
    mat4 view_inv;
    mat4 proj;
    mat4 proj_inv;
} camera;

layout(set = 3, binding = 0) uniform accelerationStructureEXT scene;

layout(set = 4, binding = 0, rgba8) uniform image2D image;

layout(push_constant) uniform PushConstant {
    uint renderMode;
} pushConstant;

layout(location = 0) rayPayloadEXT RayPayload rayPayload;

void main()
{
	const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 inUV = pixelCenter / vec2(gl_LaunchSizeEXT.xy);
	vec2 d = inUV * 2.0 - 1.0;

	vec4 origin = camera.view_inv * vec4(0.0, 0.0, 0.0, 1.0);
	vec4 target = camera.proj_inv * vec4(d.x, d.y, 1.0, 1.0);
	vec4 direction = camera.view_inv * vec4(normalize(target.xyz), 0.0);

	float tMin = 0.001;
	float tMax = 10000.0;

    traceRayEXT(scene, gl_RayFlagsOpaqueEXT, 0xff, 0, 0, 0, origin.xyz, tMin, direction.xyz, tMax, 0);

	vec3 color = rayPayload.color;
	if (pushConstant.renderMode == 1) {
		color = rayPayload.normal * 0.5 + 0.5;
	}

	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(color, 0.0));
}
